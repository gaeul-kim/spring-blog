buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.franzbecker:gradle-lombok:1.8")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.franzbecker.gradle-lombok'

group = 'xyz.sangsik'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    def querydslVersion = "4.1.4"

    testCompile('org.springframework.boot:spring-boot-starter-test')
    compileOnly('org.projectlombok:lombok')

    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-devtools')
    compile("com.querydsl:querydsl-core:$querydslVersion")
    compile("com.querydsl:querydsl-apt:$querydslVersion")
    compile("com.querydsl:querydsl-jpa:$querydslVersion")
    runtime('org.hsqldb:hsqldb')
    runtime('mysql:mysql-connector-java')


    /* front-end lib dependency */
    compile('org.webjars.bower:bootstrap:4.0.0-beta.3')
    compile('org.webjars.bower:jquery:3.2.1')

    /* markdown, parser lib */
    compile group: 'com.youbenzi', name: 'MDTool', version: '1.1.4'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.2'

    /* for legacy html in thymeleaf */
    compile(group: 'net.sourceforge.nekohtml', name: 'nekohtml', version: '1.9.22')

    /* test */
    compile('com.thedeanda:lorem:2.1')

}

/**** QueryDSL Class Generate Script ****/
def queryDslOutput = file("src/main/generated")
sourceSets {
    main {
        java {
            srcDir queryDslOutput
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile
    destinationDir = queryDslOutput
    options.compilerArgs = [
            "-proc:only",
            "-processor", 'com.querydsl.apt.jpa.JPAAnnotationProcessor'
    ]
}
compileJava.dependsOn(generateQueryDSL)

clean {
    delete queryDslOutput
}
